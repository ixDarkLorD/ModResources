apply plugin: "com.github.johnrengelman.shadow"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    // Loom sets the log4j config while in development (in prod the log4j config might be dictated by the launcher or just the game itself).
    // This loom gradle config specifies that loom uses our customized log4j config file.
    // ref: https://fabricmc.net/wiki/documentation:fabric_loom
    // (although we're using the architectury fork of fabric loom, this config remains unchanged)
    log4jConfigs.from project(":common").file("/src/main/resources/log4j.xml")
    
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        configureEach {
            ideConfigGenerated(true)
            runDir("../run")
            vmArgs "-Xms${runtime_min_ram}", "-Xmx${runtime_max_ram}"
            property 'mixin.debug.export', mixin_debug
            property 'fabric-tag-conventions-v2.missingTagTranslationWarning', 'silenced'
            property 'fabric-tag-conventions-v1.legacyTagWarning', 'silenced'
        }
        client {
            client()
            setConfigName("Fabric Client ${libs.versions.minecraft.get()}")
        }
        server {
            server()
            setConfigName("Fabric Server ${libs.versions.minecraft.get()}")
        }
        data {
            inherit client
            setConfigName("Fabric Data ${libs.versions.minecraft.get()}")
            property("fabric-api.datagen")
            property("fabric-api.datagen.output-dir", file("src/generated/resources").absolutePath)
            property("fabric-api.datagen.modid", modid)
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "Ladysnake Libs"
        url = "https://maven.ladysnake.org/releases"
    }
    maven {
        name = "jamieswhiteshirt"
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
    }
}

dependencies {
    // Common Project
    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    // Fabric Loader
    modApi libs.fabric.loader

    // Fabric Api
    modApi libs.fabric.api

    if (libs.hasProperty("modmenu") && libs.modmenu.hasProperty("fabric")) {
        modLocalRuntime(libs.modmenu.fabric) { transitive = false }
    }
}

tasks.withType(Jar).configureEach {
    exclude "architectury.common.json"
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = ""
    injectAccessWidener = true
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}